// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© ZenAndTheArtOfTrading / www.PineScriptMastery.com
// @version=5
indicator("Session Candles", overlay=true)

// Get user input
session_ny = input.session(title="U.S. Session", defval="0900-2300")
session_tokyo = input.session(title="Tokyo Session", defval="1900-0500")
session_london = input.session(title="London Session", defval="0400-1400")

// Check if the current bar falls within the given time session
isInSession(_sess) => not na(time(timeframe.period, _sess))

//===== TOKYO SESSION =====//
startedTK = isInSession(session_tokyo) and not isInSession(session_tokyo)[1]
var saveTK = false
exitedTK = isInSession(session_tokyo)[1] and not isInSession(session_tokyo)
var tkHigh = 0.0
var tkLow = 999999999.9
var tkOpen = 0.0
var tkClose = 0.0

// If we've entered Tokyo session, prepare our variables
if startedTK
    saveTK := true
    tkOpen := open
    tkHigh := 0.0
    tkLow := 999999999.9

// If we've exited the Tokyo session, save the close
if exitedTK
    tkClose := close[1]
    saveTK := false

// If we're within the Tokyo session, save the highest high/lowest low
if saveTK
    if high > tkHigh
        tkHigh := high
    if low < tkLow
        tkLow := low

// Plot the bar to our chart
plotbar(exitedTK ? tkOpen : na, exitedTK ? tkHigh : na, exitedTK ? tkLow : na, exitedTK ? tkClose : na, color=color.purple, title="Tokyo Session")
//===== END TOKYO SESSION =====//

//===== LONDON SESSION =====//
startedLD = isInSession(session_london) and not isInSession(session_london)[1]
var saveLD = false
exitedLD = isInSession(session_london)[1] and not isInSession(session_london)
var ldHigh = 0.0
var ldLow = 9999999.9
var ldOpen = 0.0
var ldClose = 0.0

// If we've entered London session, prepare our variables
if startedLD
    saveLD := true
    ldOpen := open
    ldHigh := 0.0
    ldLow := 9999999.9

// If we've exited London session, save the close
if exitedLD
    saveLD := false
    ldClose := close[1]

// If we're within the London session, save the highest high/lowest low
if saveLD
    if high > ldHigh
        ldHigh := high
    if low < ldLow
        ldLow := low

// Plot the bar to our chart    
plotbar(exitedLD ? ldOpen : na, exitedLD ? ldHigh : na, exitedLD ? ldLow : na, exitedLD ? ldClose : na, color=color.orange, title="London Session")
//===== END LONDON SESSION =====//

//===== NY SESSION =====//
startedNY = isInSession(session_ny) and not isInSession(session_ny)[1]
var saveNY = false
exitedNY = isInSession(session_ny)[1] and not isInSession(session_ny)
var nyHigh = 0.0
var nyLow = 9999999.9
var nyOpen = 0.0
var nyClose = 0.0

// If we've entered New York session, prepare our variables
if startedNY
    saveNY := true
    nyOpen := open
    nyHigh := 0.0
    nyLow := 9999999.9

// If we've exited New York session, save the close
if exitedNY
    saveNY := false
    nyClose := close[1]

// If we're within the New York session, save the highest high/lowest low
if saveNY
    if high > nyHigh
        nyHigh := high
    if low < nyLow
        nyLow := low

// Plot the bar to our chart   
plotbar(exitedNY ? nyOpen : na, exitedNY ? nyHigh : na, exitedNY ? nyLow : na, exitedNY ? nyClose : na, color=color.blue, title="New York Session")
//===== END NY SESSION =====//